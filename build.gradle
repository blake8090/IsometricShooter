import io.github.fvarrui.javapackager.gradle.PackageTask

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'io.github.fvarrui:javapackager:1.6.6'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.20'
}

apply plugin: 'io.github.fvarrui.javapackager.plugin'

repositories {
    mavenCentral()
}

group = 'bke.iso'
version = '1.0'

dependencies {
    implementation 'io.github.libktx:ktx-async:1.10.0-rc1'

    implementation 'com.badlogicgames.gdx:gdx-backend-lwjgl3:1.10.0'
    implementation 'com.badlogicgames.gdx:gdx-platform:1.10.0:natives-desktop'

    implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.2.11'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.11'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.36'

    implementation 'org.reflections:reflections:0.10.2'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.6.21'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.assertj:assertj-core:3.22.0'
    testImplementation 'org.mockito:mockito-core:4.5.1'
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

task packageWindowsExe(type: PackageTask, dependsOn: build) {
    mainClass = 'bke.iso.MainKt'
    bundleJre = true
    generateInstaller = false
    administratorRequired = false
    platform = io.github.fvarrui.javapackager.model.Platform.windows
    additionalResources = [file('assets')]
}
