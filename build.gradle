import io.github.fvarrui.javapackager.gradle.PackageTask

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'io.github.fvarrui:javapackager:1.6.6'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.22'
    id 'application'
}

apply plugin: 'io.github.fvarrui.javapackager.plugin'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

group = 'bke.iso'
version = '1.0'

dependencies {
    implementation 'io.github.libktx:ktx-async:1.10.0-rc1'

    implementation 'com.badlogicgames.gdx:gdx-backend-lwjgl3:1.11.0'
    implementation 'com.badlogicgames.gdx:gdx-platform:1.11.0:natives-desktop'

    implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.2.11'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.11'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.36'

    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-toml', version: '2.14.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.14.0'

    implementation 'org.reflections:reflections:0.10.2'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.7.22'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'

    implementation "space.earlygrey:shapedrawer:2.5.0"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.assertj:assertj-core:3.23.1'
    testImplementation 'org.mockito:mockito-core:4.10.0'
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

application {
    mainClass = "bke.iso.MainKt"
}

tasks.register('start', JavaExec) {
    group "Application"
    description "First run tests, then run application"
    dependsOn test
    mainClass.set("bke.iso.MainKt")
    classpath = sourceSets.main.runtimeClasspath
}

task packageWindowsExe(type: PackageTask, dependsOn: build) {
    mainClass = 'bke.iso.MainKt'
    bundleJre = true
    generateInstaller = false
    administratorRequired = false
    platform = io.github.fvarrui.javapackager.model.Platform.windows
    additionalResources = [file('assets')]
}
